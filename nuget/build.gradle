import java.util.regex.Matcher;
import java.util.regex.Pattern;

plugins {
    id "com.jfrog.artifactory" version "5.2.5" apply false
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

publishing {
  repositories {
    maven {
      url = "${System.getProperty('user.home')}/releases/maven/development"
    }
  }
}

if (System.getenv()['RUN_AZURE_ARTIFACTORY_RELEASE'] != null) {
  artifactory {
    contextUrl = 'https://frcmaven.wpi.edu/artifactory' // base artifactory url
    publish {
      repository {
        repoKey = 'thirdparty-mvn-release'
        username = System.getenv()['ARTIFACTORY_PUBLISH_USERNAME']
        password = System.getenv()['ARTIFACTORY_PUBLISH_PASSWORD']
      }
    }
    if (project.hasProperty('buildName')) {
      clientConfig.info.setBuildName(project.getProperty('buildName'))
    } else {
      clientConfig.info.setBuildName('thirdparty')
    }
  }
  publish.dependsOn artifactoryPublish
}

def products = file('products')

def pubVersion = file('products/version.txt').text.trim().replaceAll('-','.')

def foundArtifacts = []
def foundStaticArtifacts = []

products.eachFile {
    if (it.name.contains('sharedcrt')) {
        foundArtifacts << it
    }
    if (it.name.contains('staticcrt')) {
        foundStaticArtifacts << it
    }
}

task unzipAll() {

}

foundArtifacts.each { artifact ->
    def unzipTask = project.tasks.create("unzip${artifact.name.capitalize()}", Copy) {
        from zipTree(artifact)
        into file("${buildDir}/unzipped/shared")
    }
    unzipAll.dependsOn unzipTask
}

foundStaticArtifacts.each { artifact ->
    def unzipTask = project.tasks.create("unzip${artifact.name.capitalize()}", Copy) {
        from zipTree(artifact)
        into file("${buildDir}/unzipped/static")
    }
    unzipAll.dependsOn unzipTask
}

def artifactMap = [
    "${buildDir}/unzipped/shared/windows/x86-64/shared/SDL3.dll": "runtimes/win-x64/native",
    "${buildDir}/unzipped/shared/windows/arm64/shared/SDL3.dll": "runtimes/win-arm64/native",
    "${buildDir}/unzipped/shared/osx/universal/shared/libSDL3.dylib": "runtimes/osx-arm64/native",
    "${buildDir}/unzipped/shared/linux/x86-64/shared/libSDL3.so": "runtimes/linux-x64/native",
]

def staticArtifactMap = [
    "${buildDir}/unzipped/static/windows/x86-64/static/SDL3.lib": "runtimes/win-x64",
    "${buildDir}/unzipped/static/windows/arm64/static/SDL3.lib": "runtimes/win-arm64",
    "${buildDir}/unzipped/static/osx/universal/static/libSDL3.a": "runtimes/osx-arm64",
    "${buildDir}/unzipped/static/linux/x86-64/static/libSDL3.a": "runtimes/linux-x64",
]

task copyArtifacts(type: Copy) {
    dependsOn unzipAll
    destinationDir = file("${projectDir}/WPILib.DriverStation.SDL.runtime")
    artifactMap.each { src, dest ->
        if (src.contains("libSDL3.dylib")) {
            from (src) {
                into "runtimes/osx-x64/native"
            }
        }
        from (src) {
            into dest
        }
    }
}

task copyStaticArtifacts(type: Copy) {
    dependsOn unzipAll
    destinationDir = file("${projectDir}/WPILib.DriverStation.SDL.staticruntime")
    staticArtifactMap.each { src, dest ->
        if (src.contains("universal/static/libSDL3.a")) {
            from (src) {
                into "runtimes/osx-x64"
            }
        }
        from (src) {
            into dest
        }
    }
}

project.tasks.create('generateNugetPackage', Exec) {
    dependsOn copyArtifacts

    executable 'dotnet'
    args 'pack'
    args 'WPILib.DriverStation.SDL.runtime/WPILib.DriverStation.SDL.runtime.csproj'
    args '-o', file("${buildDir}/nugetpackages")
    args "/p:Version=${pubVersion}"
}

project.tasks.create('generateStaticNugetPackage', Exec) {
    dependsOn copyStaticArtifacts

    executable 'dotnet'
    args 'pack'
    args 'WPILib.DriverStation.SDL.staticruntime/WPILib.DriverStation.SDL.staticruntime.csproj'
    args '-o', file("${buildDir}/nugetpackages")
    args "/p:Version=${pubVersion}"
}

project.tasks.create('generateNugetPackages') {
    dependsOn generateNugetPackage
    dependsOn generateStaticNugetPackage
}

project.tasks.create('publishNugetPackages', Exec) {
    dependsOn generateNugetPackages

    executable 'dotnet'
    args 'nuget'
    args 'push'
    args "${buildDir}/nugetpackages/WPILib.DriverStation.SDL.runtime.${pubVersion}.nupkg"
    args '--source'
    args "https://wpilib.jfrog.io/artifactory/api/nuget/wpilib-nuget-development-local/WPILib.DriverStation.SDL.runtime/${pubVersion}"
    args '--api-key'
    args (System.getenv('ARTIFACTORY_PUBLISH_USERNAME') + ':' + System.getenv('ARTIFACTORY_PUBLISH_PASSWORD'))

    executable 'dotnet'
    args 'nuget'
    args 'push'
    args "${buildDir}/nugetpackages/WPILib.DriverStation.SDL.staticruntime.${pubVersion}.nupkg"
    args '--source'
    args "https://wpilib.jfrog.io/artifactory/api/nuget/wpilib-nuget-development-local/WPILib.DriverStation.SDL.staticruntime/${pubVersion}"
    args '--api-key'
    args (System.getenv('ARTIFACTORY_PUBLISH_USERNAME') + ':' + System.getenv('ARTIFACTORY_PUBLISH_PASSWORD'))
}
